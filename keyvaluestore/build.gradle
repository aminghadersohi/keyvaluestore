/*
 * Copyright (C) 2018 Ludwig
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath rootProject.ext.kotlinPlugin
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.6'
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api rootProject.ext.rxJava

    testImplementation rootProject.ext.junit
    testImplementation rootProject.ext.truth
    testImplementation rootProject.ext.kotlinStdlib
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        KeyValueStore(MavenPublication) {
            from components.java
            groupId GROUP
            artifactId 'keyvaluestore'
            version VERSION_NAME
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'KeyValueStore'
                    description DESCRIPTION
                    url PROJECT_URL
                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }
                    scm {
                        url PROJECT_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['KeyValueStore']
    pkg {
        repo = BINTRAY_REPO
        name = BINTRAY_NAME
        userOrg = ORGANISATION
        licenses = [LICENSE]
        desc = DESCRIPTION
        websiteUrl = PROJECT_URL
        issueTrackerUrl = ISSUE_TRACKER_URL
        vcsUrl = PROJECT_URL
    }
}